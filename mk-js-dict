#!/usr/bin/perl
# Make a javascript trie representing a dictionary
# James Stanley 2014

use strict;
use warnings;

use JSON;

my $dictionary = {};

while (<>) {
    chomp;

    my ($w, $count) = split /\s+/;

    add_word($w => $count);
}

my $jsondict = encode_json($dictionary);
$jsondict =~ s/"//g;
my $x = '{0:1,s:{0:1}}';
$jsondict =~ s/\Q$x\E/x/g;
my $y = '{i:{n:{g:{0:1}}}}';
$jsondict =~ s/\Q$y\E/y/g;
my $z = '{0:1}';
$jsondict =~ s/\Q$z\E/z/g;

my $morevars = '';
for my $l ('a'..'z') {
    my $u = uc $l;
    my $t = "{$l:z}";
    $jsondict =~ s/\Q$t\E/$u/g;
    $morevars .= "var $u=$t;";
}

print "var x=$x;var y=$y;var z=$z;${morevars}var dictionary=$jsondict;\n";

sub add_word {
    my ($word, $count) = @_;

    my $n = $dictionary;

    while (length $word) {
        # TODO: deal with invalid letters

        $word =~ s/^(.)//;
        my $c = $1;

        if (!$n->{$c}) {
            $n->{$c} = {};
        }

        $n = $n->{$c};
    }

    $n->{0} = $count ? $count : 1;
}
